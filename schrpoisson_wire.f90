subroutine el_field_slab(x,a,lambda,eps,elfield)
  implicit none
  ! El. field (along x) of a slab of thickness a along z, infinitely extended along y,
  ! put at x=0, calculated at the point (x,0,0)
  double precision, intent(in) :: x ! distance from the slab, in angstrom
  double precision, intent(in) :: a ! thickness along z, in angstrom
  double precision, intent(in) :: lambda ! linear charge density in e/cm
  double precision, intent(in) :: eps ! relative dielectric constant at the point at which
                                      ! we want to calculate the electric field (adimensional)
  double precision :: elfield ! V/ang

  double precision :: PI, FOURPIEPS0
  parameter(PI=3.1415926535897932d0)
  ! FOURPIEPS0 = 4*pi*epsilon_0 in e/(volt * cm)
  parameter(FOURPIEPS0=6944615.72)

  if (x .gt. 0) then
     elfield = (4.d0*lambda*acos((2.d0*x)/Sqrt(a**2 + 4.d0*x**2)))/a
  elseif (x.eq.0) then
     elfield = 0.d0
  else
     elfield = 4.d0*lambda*(-PI + acos((2.d0*x)/Sqrt(a**2 + 4.d0*x**2)))/a
  end if

  ! with the given units, I get in this way the electric field in V/ang
  elfield = elfield / FOURPIEPS0 / eps

end subroutine el_field_slab

subroutine v_of_rho(v,x,lambda,eps,a,n)
  implicit none
  ! return the potential of a given density of wires. Set the potential to zero at left edge.
  ! assumes that x is sorted.
  
  ! INPUT
  ! x:      array with the discretization along x in angstrom
  ! lambda: linear charge density in e/cm on the grid defined by x
  ! eps:    relative dielectric constant on the grid defined by x
  ! a:      thickness of the slab in angstrom
  ! n:      number of elements of the array

  ! OUTPUT
  ! v:      electrostatic energy in eV
  double precision, intent(out), dimension(n) :: v
  double precision, intent(in), dimension(n) :: x
  double precision, intent(in), dimension(n) :: lambda
  double precision, intent(in), dimension(n) :: eps
  double precision, intent(in) :: a
  integer, intent(in) :: n

  integer :: i, j
  
  double precision, dimension(n) :: efield ! stores temporarily the el. field
  double precision :: this_efield

  efield = 0.d0
  v = 0.d0

  ! I calculate the electric field in V/angstrom
  do i=1, n
     do j=1,n
        if (i.eq.j) cycle
        ! el.field generated at pos. i, generated by the slab with linear charge density lambda at pos. j
        call el_field_slab(x(i)-x(j),a,lambda(j),eps(i),this_efield)
        efield(i) = efield(i) + this_efield
     end do
  end do

  do i=1, n
     do j=1, i-1
     ! v is the integral of efield (times e); I set to zero at left edge
     ! here it is e * V/angstrom * angstrom = eV
     v(i) = v(i) + efield(j) * (x(i) - x(j))
     end do
  end do

end subroutine v_of_rho

